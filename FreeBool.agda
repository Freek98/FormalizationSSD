{-# OPTIONS --cubical --guardedness #-}

module FreeBool where

{- TODO : write header, don't forget to credit David -}

open import Cubical.Foundations.Prelude
open import Cubical.Foundations.Structure
open import Cubical.Foundations.Equiv
open import Cubical.Foundations.Isomorphism
open import Cubical.Foundations.Function

open import Cubical.HITs.PropositionalTruncation as PT
open import Cubical.Functions.Surjection

open import Cubical.Algebra.BooleanRing
open import Cubical.Algebra.BooleanRing.Initial
open import Cubical.Algebra.BooleanRing.Instances.Bool
open import Cubical.Data.Bool renaming (elim to bool-ind)
open import Cubical.Data.Sigma
open import Cubical.Algebra.CommRing
open import Cubical.Tactics.CommRingSolver

open import Cubical.Algebra.CommRing.Ideal
open import Cubical.Algebra.CommRing.Instances.Bool
open import Cubical.Algebra.CommRing.Quotient.ImageQuotient as IQ
open import Cubical.Algebra.Ring.Properties
open RingTheory

import Cubical.HITs.SetQuotients.Base as SQ
import Cubical.HITs.SetQuotients.Properties as SQ

open import Cubical.Algebra.CommRing.Ideal
open import Cubical.Algebra.CommRing.Quotient.Base

open import Cubical.Algebra.CommRing.Polynomials.Typevariate.Base
open import Cubical.Algebra.CommRing.Polynomials.Typevariate.UniversalProperty as TV

private module _ {ℓ : Level} (R : CommRing ℓ) where
  open CommRingStr (snd R)

  isIdemRing' : Type ℓ
  isIdemRing' = ∀ (r : ⟨ R ⟩) → r · r - r ≡ 0r
  
  IdemRing'→ : isIdemRing' → isIdemRing R
  IdemRing'→ p r = equalByDifference (CommRing→Ring R) _ _ (p r)

  IdemRing→' : isIdemRing R → isIdemRing'
  IdemRing→' p r = r · r - r ≡⟨ cong (λ a → a - r) (p r) ⟩ r - r ≡⟨ solve! R ⟩ 0r ∎

module _ {ℓ : Level} (R : CommRing ℓ) where
  open CommRingStr ⦃...⦄
  instance
    _ = snd R

  idemFunc : ⟨ R ⟩ → ⟨ R ⟩
  idemFunc a = a · a - a

  idemIdeal : IdealsIn R
  idemIdeal = genIdeal R idemFunc

  _/Idem : CommRing ℓ
  _/Idem = R /Im idemFunc

  _/IdemisIdem : isIdemRing _/Idem
  _/IdemisIdem = IdemRing'→ _/Idem (SQ.elimProp (λ y → CommRingStr.is-set (str _/Idem) _ _) (zeroOnImage _ _ ))

  forceBA : BooleanRing ℓ
  forceBA = idemCommRing→BR _/Idem _/IdemisIdem 

  module _ {ℓ' : Level} (B : BooleanRing ℓ') (f : CommRingHom R (BooleanRing→CommRing B)) where

    instance
      _ = (snd (BooleanRing→CommRing B))

    open IsCommRingHom (snd f)

    mapsIntoBAsAnnihilatesIdemFunc : (x : ⟨ R ⟩) →
                                     f $cr (idemFunc x) ≡ 0r
    mapsIntoBAsAnnihilatesIdemFunc x = f $cr ( x · x - x)
                                         ≡⟨ pres+ _ _ ⟩
                                       f $cr (x · x) + f $cr (- x )
                                         ≡⟨ cong (λ a → _ + a) (pres- _) ⟩
                                       f $cr (x · x) - f $cr x
                                         ≡⟨ cong (λ a → a - _) (pres· _ _) ⟩
                                       f $cr x · f $cr x - f $cr x
                                         ≡⟨ IdemRing→' (BooleanRing→CommRing B) (BooleanRingStr.·Idem (snd B)) (f $cr x) ⟩
                                       0r ∎

opaque 
  freeBA : {ℓ : Level} → (A : Type ℓ) → BooleanRing ℓ
  freeBA A = forceBA (BoolCR [ A ])

opaque
  unfolding freeBA
  generator : {ℓ : Level} → {A : Type ℓ} → A → ⟨ freeBA A ⟩
  generator a = quotientHom _ (idemIdeal _) $cr var a

module _ {ℓ : Level} (A : Type ℓ) (B : BooleanRing ℓ) (f : A → ⟨ B ⟩) where
  open IsCommRingHom
  open CommRingStr ⦃...⦄
  instance
    _ = str (BooleanRing→CommRing B)

  private
    2[A] : CommRing _
    2[A] = BoolCR [ A ]

    2[A]→B : (CommRingHom 2[A] (BooleanRing→CommRing B) )
    2[A]→B = TV.inducedHom (BooleanRing→CommRing B) (BoolBR→ B) f

    idem : ⟨ 2[A] ⟩ → ⟨ 2[A] ⟩
    idem = idemFunc 2[A]
    
    2[A]→BRespIdem : (x : ⟨ 2[A] ⟩) → 2[A]→B $cr (idem x) ≡ 0r
    2[A]→BRespIdem = mapsIntoBAsAnnihilatesIdemFunc _ B 2[A]→B
  
  instance
    _ = str 2[A]
  opaque
    unfolding freeBA
    unfolding IQ.inducedHom
    inducedBAHom : BoolHom (freeBA A) B
    inducedBAHom = IQ.inducedHom 2[A] idem 2[A]→B 2[A]→BRespIdem 

  opaque
    unfolding freeBA 
    unfolding generator
    unfolding inducedBAHom
    inducedBAHomUnique : (g : BoolHom (freeBA A) B) →
                         ((fst g) ∘ generator ≡ f)  →
                         inducedBAHom ≡ g
    inducedBAHomUnique g agreeOnGens = IQ.inducedHomUnique 2[A] idem 2[A]→B 2[A]→BRespIdem g 2[A]→B=gq where
 
      g∘quot : CommRingHom 2[A] (BooleanRing→CommRing B)
      g∘quot = g ∘cr quotientHom _ _ 
 
      2[A]→B=gq : 2[A]→B ≡ g∘quot
      2[A]→B=gq = TV.inducedHomUnique _ _ f g∘quot (BoolBR→IsUnique B (g∘quot ∘cr constPolynomial BoolCR A)) agreeOnGens


module _ {ℓ : Level} (A : Type ℓ) (B : BooleanRing ℓ) (f : A → ⟨ B ⟩) where
  opaque 
    unfolding generator 
    unfolding inducedBAHom
    unfolding IQ.inducedHom 
    evalBAInduce : fst (inducedBAHom A B f) ∘ generator ≡ f
    evalBAInduce = TV.evalInduce _ _ f

freeBA-universal-property : {ℓ : Level} → (A : Type ℓ) → (B : BooleanRing ℓ) → 
                            Iso (A → ⟨ B ⟩)
                            (BoolHom (freeBA A) B)
Iso.fun      (freeBA-universal-property A B)   = inducedBAHom A B
Iso.inv      (freeBA-universal-property A B) f = fst f ∘ generator
Iso.rightInv (freeBA-universal-property A B) f = inducedBAHomUnique A B (fst f ∘ generator) f refl 
Iso.leftInv  (freeBA-universal-property A B)   = evalBAInduce _ _ 

